in HIGHP vec4 in_position : POSITION;
in MEDIUMP vec2 in_st : TEXCOORD0;
in LOWP vec4 in_tangent : TEXCOORD1;
in LOWP vec4 in_color : COLOR;
in LOWP vec3 in_normal : NORMAL;

#ifdef USE_SHADOW_MAP
$include "ShadowLibrary.vp"
#endif

out LOWP vec4 v2f_color;
out MEDIUMP vec2 v2f_tex;

#if _NORMAL == 0
    out LOWP vec3 v2f_normal;
#endif

#ifdef DIRECT_LIGHTING
    out vec3 v2f_lightVector;
    out vec3 v2f_lightFallOff;
    out vec4 v2f_lightProjection;
#endif

#ifdef INDIRECT_LIGHTING
    out vec4 v2f_toWorldAndPackedWorldPosS;
    out vec4 v2f_toWorldAndPackedWorldPosT;
    out vec4 v2f_toWorldAndPackedWorldPosR;
#endif

#if defined(INDIRECT_LIGHTING) || defined(DIRECT_LIGHTING) || _PARALLAX != 0
    out vec3 v2f_viewVector;
#endif

#ifdef INSTANCED_DRAW
    struct entityData_t {
        vec4 worldMatrixS;
        vec4 worldMatrixT;
        vec4 worldMatrixR;
        vec3 localViewOrigin;
        vec3 localLightDir;
        vec4 textureMatrixS;
        vec4 textureMatrixT;
        vec4 vertexColorScale;
        vec4 vertexColorAdd;
        vec4 constantColor;
    };

    uniform entityData_t entityData[256];
    #define ED entityData[gl_InstanceIDARB]
#else
    uniform vec4 worldMatrixS;
    uniform vec4 worldMatrixT;
    uniform vec4 worldMatrixR;
    uniform vec3 localViewOrigin;
    uniform mat3 localLightAxis;
    uniform vec4 localLightOrigin;
    uniform vec4 textureMatrixS;
    uniform vec4 textureMatrixT;
    uniform LOWP vec4 vertexColorScale;
    uniform LOWP vec4 vertexColorAdd;
    uniform LOWP vec4 constantColor;
#endif

uniform mat4 modelViewProjectionMatrix;
uniform vec3 lightInvRadius;
uniform mat4 lightTextureMatrix;
uniform bool useShadowMap;

void main() {
#if _NORMAL == 0
    vec4 localVertex;
    vec3 localNormal;

    #ifdef COMPRESSED_VERTEX_NORMAL_TANGENTS
        vec3 vNormal = in_normal.xyz * 2.0 - 1.0;
    #else
        vec3 vNormal = in_normal.xyz;
    #endif
    
    #ifdef GPU_SKINNING
        skinningMatrix(in_position, localVertex, vNormal, localNormal);
    #else
        localVertex = in_position;
        localNormal = vNormal;
    #endif
#else
    vec4 localVertex;
    vec3 localNormal;
    vec3 localTangent;
    vec3 localBiTangent;

    #ifdef COMPRESSED_VERTEX_NORMAL_TANGENTS
        vec3 vNormal = in_normal.xyz * 2.0 - 1.0;
        vec4 vTangent = in_tangent * 2.0 - 1.0;
    #else
        vec3 vNormal = in_normal.xyz;
        vec4 vTangent = in_tangent;
    #endif

    #ifdef GPU_SKINNING
        skinningMatrix(in_position, localVertex, vNormal, localNormal, vTangent, localTangent, localBiTangent);
    #else
        localVertex = in_position;
        localNormal = vNormal;
        localTangent = vTangent.xyz;
        localBiTangent = normalize(cross(vNormal, vTangent.xyz) * vTangent.w);
    #endif
#endif

#if defined(INDIRECT_LIGHTING) || defined(DIRECT_LIGHTING)
    vec4 worldVertex;
    worldVertex.x = dot(worldMatrixS, localVertex);
    worldVertex.y = dot(worldMatrixT, localVertex);
    worldVertex.z = dot(worldMatrixR, localVertex);
    worldVertex.w = 1.0;
#endif

#ifdef DIRECT_LIGHTING
    #ifdef USE_SHADOW_MAP
        #ifdef USE_SHADOW_POINT
            ShadowCubeTransform(localVertex, localLightOrigin.xyz, worldMatrixS.xyz, worldMatrixT.xyz, worldMatrixR.xyz);
        #elif defined(USE_SHADOW_SPOT) || defined(USE_SHADOW_CASCADE)
            ShadowTransform(worldVertex);
        #endif
    #endif
    
    v2f_lightProjection = lightTextureMatrix * worldVertex;

    vec3 L = localLightOrigin.xyz + (localLightOrigin.w - 1.0) * localVertex.xyz;

    v2f_lightFallOff = (L * localLightAxis) * lightInvRadius;
#endif

#if _NORMAL == 0
    #if defined(DIRECT_LIGHTING) || defined(INDIRECT_LIGHTING) || _PARALLAX != 0
        v2f_normal = localNormal;

        v2f_viewVector = localViewOrigin.xyz - localVertex.xyz;
    #endif

    #ifdef DIRECT_LIGHTING
        v2f_lightVector = L;
    #endif
   
    #ifdef INDIRECT_LIGHTING
        v2f_toWorldAndPackedWorldPosS.xyz = worldMatrixS.xyz;
        v2f_toWorldAndPackedWorldPosT.xyz = worldMatrixT.xyz;
        v2f_toWorldAndPackedWorldPosR.xyz = worldMatrixR.xyz;

        v2f_toWorldAndPackedWorldPosS.w = worldVertex.x;
        v2f_toWorldAndPackedWorldPosT.w = worldVertex.y;
        v2f_toWorldAndPackedWorldPosR.w = worldVertex.z;
    #endif
#else
    #if defined(DIRECT_LIGHTING) || defined(INDIRECT_LIGHTING) || _PARALLAX != 0
        mat3 TBN = mat3(localTangent, localBiTangent, localNormal);

        v2f_viewVector = (localViewOrigin.xyz - localVertex.xyz) * TBN;
    #endif

    #ifdef DIRECT_LIGHTING
        v2f_lightVector = L * TBN;
    #endif
    
    #ifdef INDIRECT_LIGHTING
        v2f_toWorldAndPackedWorldPosS.xyz = worldMatrixS.xyz * TBN;
        v2f_toWorldAndPackedWorldPosT.xyz = worldMatrixT.xyz * TBN;
        v2f_toWorldAndPackedWorldPosR.xyz = worldMatrixR.xyz * TBN;
        //v2f_toWorldAndPackedWorldPosT.w = in_tangent.w;

        v2f_toWorldAndPackedWorldPosS.w = worldVertex.x;
        v2f_toWorldAndPackedWorldPosT.w = worldVertex.y;
        v2f_toWorldAndPackedWorldPosR.w = worldVertex.z;
    #endif
#endif

    v2f_tex.x = dot(textureMatrixS, vec4(in_st, 0.0, 1.0));
    v2f_tex.y = dot(textureMatrixT, vec4(in_st, 0.0, 1.0));

#ifdef LIGHTMAP
    v2f_tex.zw = in_st2.xy;
#endif

    v2f_color = (in_color * vertexColorScale + vertexColorAdd) * constantColor;

    gl_Position = modelViewProjectionMatrix * localVertex;
}
